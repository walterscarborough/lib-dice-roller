buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.13'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        main.java.srcDirs += "generated-sources/main/java"

        main.proto.srcDirs += "../../../common/dice-roller/protobuf_schemata"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "com.google.protobuf:protobuf-javalite:3.9.1"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

protobuf {
    generateProtoTasks.generatedFilesBaseDir = "${project(':LibDiceRollerAdapter').getProjectDir()}/generated-sources"

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
                java {
                    option "lite"
                }
            }
        }
    }
}

task copyRustLibraryX86(type: Copy) {
    from '../../../common/dice-roller-jni/target/i686-linux-android/release/libdice_roller_jni.so'
    into "${project(':LibDiceRollerAdapter').getProjectDir()}/src/main/jniLibs/x86/"
}

preBuild.dependsOn(copyRustLibraryX86)
